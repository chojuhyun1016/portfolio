// ===== 플러그인 =====
plugins {
    id 'java'
    id 'idea'
}

group = 'org.example'
version = '1.0.0'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

// 소스셋
sourceSets {
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
    integrationTest {
        java.srcDir 'src/integrationTest/java'
        resources.srcDir 'src/integrationTest/resources'
        compileClasspath += sourceSets.main.output + configurations.runtimeClasspath + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

idea {
    module {
        testSourceDirs += file('src/integrationTest/java')
        testResourceDirs += file('src/integrationTest/resources')
    }
}

dependencies {
    // 프로젝트 모듈
    implementation(project(':order-common'))
    implementation(project(':order-core'))
    implementation(project(':order-domain'))
    implementation(project(':order-client:web'))
    implementation(project(':order-client:kafka'))
    implementation(project(':order-client:s3'))

    // main
    implementation(libs.springKafka)
    implementation(libs.springBootStarterWeb)
    implementation(libs.springBootStarterDataJpa)
    implementation(libs.querydslCore)
    implementation("${libs.querydslJpaJakarta.get().module}:${libs.querydslJpaJakarta.get().version}:jakarta")
    implementation(libs.springBootStarterValidation)
    implementation(libs.springBootStarter)
    implementation(libs.springBootStarterActuator)
    implementation(libs.springBootStarterBatch)

    // Querydsl APT
    annotationProcessor("${libs.querydslApt.get().module}:${libs.versions.querydsl.get()}:jakarta")
    annotationProcessor(libs.jakartaPersistenceApi)

    // 로깅/직렬화
    implementation(libs.logbackJsonClassic)
    implementation(libs.logbackJackson)
    implementation(libs.jacksonJsr310)
    implementation(libs.jacksonDatabind)
    implementation(libs.logstashLogbackEncoder)

    // AWS
    implementation(libs.awsMskIamAuth)
    implementation(libs.awsS3v1)
    runtimeOnly(libs.awsMysqlJdbc)

    // Lombok / Config Processor
    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
    annotationProcessor(libs.springBootConfigProcessor)

    // Unit Test
    testImplementation(libs.springBootStarterTest)
    testImplementation(libs.springKafkaTest)
    testRuntimeOnly(libs.h2)
    testImplementation(libs.springBatchTest)

    // Integration Test
    configurations.integrationTestImplementation.extendsFrom configurations.testImplementation
    configurations.integrationTestRuntimeOnly.extendsFrom configurations.testRuntimeOnly

    integrationTestImplementation(libs.springBootStarterTest)
    integrationTestImplementation(libs.springKafkaTest)

    // Testcontainers
    integrationTestImplementation(platform(libs.testcontainersBom))
    integrationTestImplementation(libs.testcontainers)
    integrationTestImplementation(libs.testcontainersJunit)
    integrationTestImplementation(libs.testcontainersMysql)
    integrationTestImplementation(libs.testcontainersJdbc)
    integrationTestRuntimeOnly(libs.mysqlDriver)

    integrationTestCompileOnly(libs.lombok)
    integrationTestAnnotationProcessor(libs.lombok)
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
tasks.named('processTestResources') { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }
tasks.matching { it.name == 'processIntegrationTestResources' }.configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events "FAILED", "SKIPPED"
        exceptionFormat "FULL"
        showExceptions true
        showStackTraces true
    }
}

test {
    systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active", "test-unit")
    systemProperty "junit.jupiter.execution.parallel.enabled", "true"
    systemProperty "junit.jupiter.execution.parallel.mode.default", "concurrent"
}

// integrationTest
def hasIntegrationTask = tasks.findByName('integrationTest') != null

if (hasIntegrationTask) {
    tasks.named('integrationTest', Test) {
        description = 'Runs integration tests (EmbeddedKafka; Testcontainers optional).'
        group = 'verification'
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        useJUnitPlatform()
        systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active", "integration")
        systemProperty "junit.jupiter.execution.parallel.enabled", "false"
        shouldRunAfter tasks.named('test')
        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "FULL"
        }
    }
} else {
    tasks.register('integrationTest', Test) {
        description = 'Runs integration tests (EmbeddedKafka; Testcontainers optional).'
        group = 'verification'
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        useJUnitPlatform()
        systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active", "integration")
        systemProperty "junit.jupiter.execution.parallel.enabled", "false"
        shouldRunAfter tasks.named('test')
        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "FULL"
        }
    }
}

// 파이프라인 의존성
check.dependsOn test
