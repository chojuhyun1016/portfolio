// ======================================================================
// module: order-api-common / build.gradle
//  - 라이브러리 모듈(부트 앱 아님): bootJar 비활성, jar 생성
//  - 버전/의존성은 libs.versions.toml (버전 카탈로그) 기준
//  - @AutoConfiguration 제공 모듈이므로 spring-boot-autoconfigure 의존 필요
//  - 설정 프로퍼티 메타데이터 생성을 위해 configuration-processor 추가(컴파일 전용)
// ======================================================================

tasks.matching { it.name == "bootJar" }.configureEach { enabled = false }
tasks.named("jar") { it.enabled = true }

dependencies {
    // --- 내부 모듈 ---
    implementation project(":order-common")
    implementation project(":order-core")

    // --- Spring Boot 스타터 (웹/시큐리티/검증/JPA) ---
    implementation(libs.springBootStarterWeb)
    implementation(libs.springBootStarterSecurity)
    implementation(libs.springBootStarterValidation)
    implementation(libs.springBootStarterDataJpa)

    // --- Auto-Configuration (@AutoConfiguration 사용 모듈) ---
    implementation(libs.springBootAutoconfigure)

    // --- Jackson (java time 포맷 커스터마이즈 시 JSR-310 모듈 확실히 포함) ---
    implementation(libs.jacksonJsr310)

    // --- Configuration Properties 메타데이터 (컴파일 전용) ---
    compileOnly(libs.springBootConfigProcessor)
    annotationProcessor(libs.springBootConfigProcessor)

    // --- Lombok (컴파일 전용) ---
    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
    testCompileOnly(libs.lombok)
    testAnnotationProcessor(libs.lombok)

    // --- spring-kafka 컴파일 전용 ---
    compileOnly(libs.springKafka)

    // --- 테스트 ---
    testImplementation(libs.springBootStarterTest)
}
