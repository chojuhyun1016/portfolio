plugins {
    id "java-library"
}

group = "org.example"
version = "1.0.0"

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    withSourcesJar()
    withJavadocJar()
}

tasks.named("jar") { enabled = true }

dependencies {
    // ===== 내부 공통 모듈 =====
    api(project(":order-common"))

    // ===== AWS SDK v2 (DynamoDB Enhanced) =====
    implementation(platform(libs.awsBomV2))
    implementation(libs.awsDynamodb)
    implementation(libs.awsDynamodbEnhanced)

    // ===== JPA & QueryDSL (Jakarta) =====
    implementation(libs.springBootStarterDataJpa)

    implementation(libs.querydslCore)
    implementation(libs.querydslJpaJakarta)

    // === APT (Q-타입 생성) ===
    annotationProcessor("com.querydsl:querydsl-apt:${libs.versions.querydsl.get()}:jakarta")

    // JPA/어노테이션 메타
    annotationProcessor(libs.jakartaPersistenceApi)
    annotationProcessor(libs.jakartaAnnotationApi)
    compileOnly(libs.jakartaPersistenceApi)
    compileOnly(libs.jakartaAnnotationApi)

    // Lombok
    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)

    // 검증 어노테이션
    compileOnly(libs.springBootStarterValidation)

    // ===== test =====
    testImplementation(libs.springBootStarterTest)
}
